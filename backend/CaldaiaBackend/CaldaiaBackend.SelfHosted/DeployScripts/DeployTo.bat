@ECHO OFF
SetLocal EnableDelayedExpansion

REM ************************************************************************************
REM This script is intended to deploy to a remote (windows) server the executables 
REM generated by building the project in which it is contained.
REM Use as a template to tailor your own deploy script.
REM ************************************************************************************

SET ServiceSuffix=Release
SET Configuration=Release

IF "%Configuration%" == "" (
	CALL :PrintUsage
	GOTO :End
)

SET CURRDIR=%~dp0

pushd %CURRDIR%

	SET ServiceName=CaldaiaBackend.SelfHosted_%ServiceSuffix%
	SET Server=192.168.2.44
	SET Username=%Server%\Administrator
	SET NetworkShareName=D$
	SET DestinationPathInShare=\%ServiceName%
	SET ServiceExecutableFullPathOnServer=D:\%ServiceName%\CaldaiaBackend.SelfHosted.exe

	REM Finds the path of the executable to deploy
	SET RelativeBinPath=%CURRDIR%..\bin\%Configuration%
	CALL :ResolvePath DeployableServicePath %RelativeBinPath%

	SET PasswordFileName=password.%ServiceSuffix%.secret
	SET /p Password=<!PasswordFileName!
	IF "!Password!" == "" ( SET PrintedPassword="- input below -" )
	IF NOT "!Password!" == "" ( SET PrintedPassword="<hidden>" )

	ECHO **************************************************************************************
	ECHO *
	ECHO * Deploying binaries
	ECHO *   BUILD CONFIG: %Configuration%
	ECHO *           FROM: %DeployableServicePath% 
	ECHO *             TO: \\%Server%\%NetworkShareName%%DestinationPathInShare%
	ECHO * PATH ON SERVER: %ServiceExecutableFullPathOnServer%
	ECHO *
	ECHO * Installing as Service
	ECHO *    SERVICENAME: %ServiceName%
	ECHO *
	ECHO * Using Credentials
	ECHO *           USER: %Username%
	ECHO *       PASSWORD: !PrintedPassword!
	ECHO *
	ECHO **************************************************************************************
	ECHO -

	IF "!Password!" == "" (
		SET /P Password=Please provide password for %Username%: 
		ECHO !Password!>!PasswordFileName!
	)

	REM Builds the project in %Configuration%
	CALL build.bat %Configuration%
	IF errorlevel 1 GOTO :End

	REM Copy the latest Frontend build into the deployable
	SET DeployableFrontendPath=%DeployableServicePath%\AngularAppDist
	robocopy ..\AngularAppDist %DeployableServicePath%\AngularAppDist /MIR /FFT /E /S /LOG:robocopy_log.txt /XD Logs /XD App_Data /W:1 /R:20
	ECHO Deployed frontend to %DeployableFrontendPath%
	type robocopy_log.txt

	REM Deploys binaries and installs the service on the remote server.
	powershell -NoProfile -ExecutionPolicy Bypass -Command .\Deploy.ps1 -Username "%Username%" -Password "%Password%" -Server "%Server%" -NetworkShareName "%NetworkShareName%" -DestinationPathInShare "%DestinationPathInShare%" -DeployableServicePath "%DeployableServicePath%" -ServiceExecutableFullPathOnServer "%ServiceExecutableFullPathOnServer%" -ServiceName "%ServiceName%"

popd

GOTO :End



REM *** F U N C T I O N S ***

REM Prints command usage
:PrintUsage
	ECHO Usage: %0 Test^|Prod
	exit /b

REM Resolves path to absolute.
REM Param 1: Name of output variable.
REM Param 2: Path to resolve.
REM Return: Resolved absolute path in output variable.
:ResolvePath
    set %1=%~dpfn2
    exit /b

:End
